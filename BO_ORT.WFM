*** bo_ort.wfm

create session

use mandant index mandant

sele 2
use pb_ort index pb_ort




** END HEADER -- Diese Zeile nicht lîschen*
* Erstellt am 18.03.1996
*

local f
f = new KATALOG_ORT()
f.ReadModal()

CLASS KATALOG_ORT OF KATALOGFORM From "BASIS.CFM"
   this.HelpFile = "PMM96.HLP"
   this.HelpID = "Standort"
   this.Text = "BOSS [ Katalog - Standorte ]"
   
   this.OnOpen = {; if glTipsUndTricks; msgbox("Jeder Standort wird einem Mandanten zugeordnet, so da· beim Editieren von PrÅfmittelkarten im Katalog immer nur die Standorte des aktuellen Mandanten angezeigt werden. Weitere Hilfe mit Taste F1...","Tips & Tricks",64); endif}
      
   this.schalter_loeschen.onclick    = class::schalter_loeschen_onclick
   this.schalter_korrigieren.onclick = class::schalter_korrigieren_onclick
   this.schalter_hinzufuegen.onclick = class::schalter_hinzufuegen_onclick


   DEFINE TEXT TEXT1 OF THIS;
       PROPERTY; 
         Width 9.002,;
         Text "Mandant",;
         Left 5.3311,;
         Top 1.7051,;
         Height 0.9395
         
   DEFINE TEXT TEXT2 OF THIS;
       PROPERTY; 
         Width 8.501,;
         Text "Standort",;
         Left 16.832,;
         Top 6.3516,;
         Height 1.0586         

   DEFINE COMBOBOX MANDANT OF THIS;
       PROPERTY; 
         OnOpen {; this.value = ""; this.visible = .t.},;
         Width 51.335,;
         Visible .F.,;
         DropDownHeight 8,;
         Left 14.3311,;
         Style 2,;
         OnChange CLASS::MANDANT_ONCHANGE,;
         DataSource "FIELD MANDANT->MAND_BEZ",;
         Top 1.4688,;
         Height 1.1768
         
   DEFINE COMBOBOX AUSWAHL OF THIS;
       PROPERTY; 
         Width 17.5,;
         Left 27.833,;
         OnChange CLASS::AUSWAHL_ONCHANGE,;
         Enabled .f.,;
         DropDownHeight 5,;
         Style 2,;
         DataSource 'ARRAY {"Einsatzorte","Lagerorte","PrÅforte","Reparaturorte","Sonstige"}',;
         Top 3.5879,;
         Height 1.1768

   DEFINE COMBOBOX PB_ORT OF THIS;
       PROPERTY; 
         Width 27.668,;
         Left 25.832,;
         Value "",;
         DropDownHeight 8,;
         Style 1,;
         Enabled .f.,;
         Top 6.1758,;
         Enabled .F.,;
         Height 1.1768

   DEFINE TEXT KORREKTUR_TEXT OF THIS;
       PROPERTY; 
         Width 20.001,;
         Visible .F.,;
         Text "Korrigierter Standort",;
         Left 13.332,;
         Top 8.5879,;
         Height 0.9404

   DEFINE ENTRYFIELD KORREKTUR_EINGABE OF THIS;
       PROPERTY; 
         Width 24.501,;
         ValidRequired .T.,;
         Valid CLASS::KORREKTUR_EINGABE_VALID,;
         Visible .F.,;
         Left 33.165,;
         MaxLength 20,;
         Top 8.4688,;
         Height 1.1182


*************************************************************************


   Procedure MANDANT_OnChange
   
    sele mandant
    set order to 1
    set exact on
    seek this.value
    set exact off
    
    form.pb_ort.visible = .f.
    sele pb_ort
    set order to 1
    set key to mandant->mand_nr
    form.pb_ort.datasource = "field pb_ort->pb_ort"
    form.pb_ort.value = ""
    form.pb_ort.visible = .t.
    
    form.pb_ort.enabled = .f.
    form.auswahl.value   = ""
    form.auswahl.enabled = .t.
    form.auswahl.setfocus()
    
    
   
   Procedure AUSWAHL_OnChange
 
    form.pb_ort.visible = .f.
    sele pb_ort
    set order to 1
    set key to mandant->mand_nr + left(form.auswahl.value,1)
    form.pb_ort.datasource = "field pb_ort->pb_ort"
    form.pb_ort.value = ""
    form.pb_ort.visible = .t.
    form.pb_ort.enabled = .t.
    form.pb_ort.setfocus()
   
   
   
   Procedure SCHALTER_LOESCHEN_OnClick
   
     if .not. empty(form.pb_ort.value)
      sele pb_ort
      * kein set order to... <-> set key to...
      seek mandant->mand_nr + left(form.auswahl.value,1) + ltrim(form.pb_ort.value)
      if .not. found()
       msgbox("  Fehler: Der Standort ist nicht vorhanden.","Achtung",48)
      else
       form.pb_ort.value = pb_ort  && wegen evtl. verkÅrzter eingabe
       if msgbox('Den Standort "' + form.pb_ort.value + '" wirklich lîschen ?',"Standort lîschen ?",292) = 6
        dele
        form.pb_ort.visible = .f.
        form.pb_ort.datasource = "field pb_ort->pb_ort"
        form.pb_ort.value = ""
        form.pb_ort.visible = .t.
       endif
      endif
     endif    
    
   
   
   Procedure SCHALTER_KORRIGIEREN_OnClick
   
     if .not. empty(form.pb_ort.value)
      sele pb_ort
      * kein set order to... <-> set key to...
      seek mandant->mand_nr + left(form.auswahl.value,1) + ltrim(form.pb_ort.value)
      if .not. found()
       msgbox("Fehler: Der Standort ist nicht vorhanden.","Achtung",48)
      else
       form.pb_ort.value = pb_ort  && wegen evtl. verkÅrzter eingabe
       form.korrektur_text.visible  = .t.
       form.korrektur_eingabe.value   = form.pb_ort.value
       form.korrektur_eingabe.visible = .t.
       form.korrektur_eingabe.setfocus()
      endif
     endif
    
   
   
    Procedure KORREKTUR_EINGABE_Valid
   
      do case
       case empty(this.value)
        this.value = form.pb_ort.value
        return .t.
       case this.value == form.pb_ort.value
        this.visible = .f.
        form.korrektur_text.visible = .f.
        form.pb_ort.setfocus()
        return .t.
       otherwise
        sele pb_ort
        * kein set order to... <-> set key to...
        local lcRettenSatznummer
        lcRettenSatznummer = recno()
        seek mandant->mand_nr + left(form.auswahl.value,1) + ltrim(this.value)
        if found()
         this.ValidErrorMsg = " Fehler: Der Standort ist schon vorhanden."
         return .f.
        else
         local lnWeiter
         lnWeiter = msgbox("Soll auch der Standort in der Kartei des betreffenden Mandanten korrigiert werden ?","PrÅfmittelkartei korrigieren ?",547)
         if lnWeiter <> 2  && kein abbruch
          go lcRettenSatznummer
          if lnWeiter = 6  && ja
           sele 5
           if alias() <> mandant->mandant
            use (mandant->mandant)
           endif 
           repl all pb_ort with ltrim(this.value) for pb_ort == pb_ort->pb_ort
           use
           sele pb_ort
          endif
          repl pb_ort with ltrim(this.value)
          * kein set order to... <-> set key to...
          form.pb_ort.visible = .f.
          form.pb_ort.datasource = "field pb_ort->pb_ort"
          form.pb_ort.value = form.korrektur_eingabe.value
          form.pb_ort.visible = .t. 
         endif 
        endif 
        form.korrektur_text.visible    = .f.
        form.korrektur_eingabe.visible = .f.
        form.pb_ort.setfocus()         
        return .t.
      endcase
      
     
    
    Procedure SCHALTER_HINZUFUEGEN_OnClick
   
     if .not. empty(form.pb_ort.value)
      sele pb_ort
      * kein set order to... <-> set key to...
      seek mandant->mand_nr + left(form.auswahl.value,1) + ltrim(form.pb_ort.value)
      if found()
       form.pb_ort.value = pb_ort  && wegen mîglicher verkÅrzter eingabe
       msgbox("  Fehler: Der Standort ist schon vorhanden.","Achtung",48)
      else
       set order to 0  && zerstîrt set key to...
       append blank
       repl mand_nr with mandant->mand_nr, art_kurz with left(form.auswahl.value,1), pb_ort with ltrim(form.pb_ort.value)
       sele pb_ort
       set order to 1
       set key to mandant->mand_nr + left(form.auswahl.value,1)
       form.pb_ort.visible = .f.
       form.pb_ort.datasource = "field pb_ort->pb_ort"
       form.pb_ort.value = ""
       form.pb_ort.visible = .t.
      endif
     endif
    
    
    
ENDCLASS
